/*
 * Copyright 2019 Davide Maestroni
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'java-library'
  id 'jacoco'
  id 'com.github.spotbugs' version '1.6.10'
  id 'com.github.ben-manes.versions' version '0.21.0'
}

apply from: '../extra.gradle'

group = project.getProperty('groupName')
version = project.getProperty('releaseVersion')
archivesBaseName = "${project.parent.name}-${project.name}"

dependencies {
  api project.hasProperty('deploy') ? "${group}:${project.parent.name}-lib:0.0.0" : project(':lib')
  api project.hasProperty('deploy') ? "${group}:${project.parent.name}-plot:0.0.0" : project(':plot')
  api project.hasProperty('deploy') ? "${group}:${project.parent.name}-remote:0.0.0" : project(':remote')
  implementation 'com.intellij:annotations:12.0'
  implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.8'
  implementation 'com.fasterxml.jackson.core:jackson-core:2.9.8'
  implementation 'com.google.code.gson:gson:2.8.5'
  implementation 'com.squareup.retrofit2:retrofit:2.5.0'
  implementation 'io.reactivex:rxjava:1.3.8'
  implementation 'io.reactivex:rxjava-math:1.0.0'
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.assertj:assertj-core:2.4.1'
}

jacoco {
  toolVersion project.getProperty('jacocoVersion')
}

jacocoTestReport {
  reports {
    xml.enabled = true // Coveralls plugin depends on xml format report
    html.enabled = true
  }
}

spotbugs {
  effort = 'max'
  reportLevel = 'low'
  ignoreFailures true
  toolVersion project.getProperty('spotbugsVersion')
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
  reports {
    xml.enabled false
    html.enabled = true
  }
}

task perf(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  main = 'dm.shakespeare.sample.MyClass2'
  jvmArgs '-agentlib:hprof=cpu=times', 'dm.shakespeare.sample.MyClass2'
}

